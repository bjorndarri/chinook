plugins {
    id 'org.beryx.jlink' version "${jlinkVersion}"
}

apply plugin: 'application'

dependencies {
    runtimeOnly "is.codion.jdk11:codion-swing-framework-server-monitor:${version}"
    runtimeOnly "is.codion.jdk11:codion-plugin-logback-proxy:${version}"
}

application {
    mainModule = "is.codion.swing.framework.server.monitor"
    getMainClass().set("is.codion.swing.framework.server.monitor.ui.EntityServerMonitorPanel")
    applicationDefaultJvmArgs = [
            "-Dcodion.server.hostname=${serverHost}",
            "-Dcodion.server.registryPort=${serverRegistryPort}",
            "-Djavax.net.ssl.trustStore=../chinook-server/config/truststore.jks",
            "-Djavax.net.ssl.trustStorePassword=crappypass",
            "-Dlogback.configurationFile=config/logback.xml",
            "-Dcodion.configurationFile=config/server_monitor.config"
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules',
               'java.naming,is.codion.plugin.logback.proxy']

    imageName = project.name + "-" + project.version

    moduleName = application.mainModule

    launcher {
        name = 'chinook-server-monitor'
        jvmArgs = [
                "-Xmx512m",
                "-Dcodion.server.hostname=${serverHost}",
                "-Dcodion.server.registryPort=${serverRegistryPort}",
                "-Djavax.net.ssl.trustStore=config/truststore.jks",
                "-Djavax.net.ssl.trustStorePassword=crappypass",
                "-Dlogback.configurationFile=config/logback.xml",
                "-Dcodion.configurationFile=config/server_monitor.config"
        ]
    }

    jpackage {
        installerType = "deb"
        installerOptions = ['--linux-shortcut']
    }
}

tasks.jlink.doLast {
    copy { from 'config' into "$buildDir/$imageName/bin/config" }
    copy { from '../chinook-server/config/truststore.jks' into "$buildDir/$imageName/bin/config" }
}