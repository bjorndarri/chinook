plugins {
    id 'org.beryx.jlink' version "${jlinkVersion}"
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply plugin: 'application'

dependencies {
    runtimeOnly "is.codion.jdk11:codion-framework-server:${version}"
    runtimeOnly "is.codion.jdk11:codion-framework-servlet:${version}"

    runtimeOnly project(":chinook-domain")

    runtimeOnly "is.codion.jdk11:codion-plugin-hikari-pool:${version}"
    runtimeOnly "is.codion.jdk11:codion-dbms-h2database:${version}"
    runtimeOnly "com.h2database:h2:1.4.200"

    //logging library, skipping the email stuff
    runtimeOnly ("is.codion.jdk11:codion-plugin-logback-proxy:${version}") {
        exclude group: 'com.sun.mail', module: 'javax.mail'
    }
}

application {
    mainClassName = "is.codion.framework.server.EntityServer"
    applicationDefaultJvmArgs = [
            "-Xmx256m",
            "-Djava.rmi.server.hostname=${serverHost}",
            "-Dcodion.configurationFile=config/h2_embedded.config",
            "-Dlogback.configurationFile=config/logback.xml",
            "-Djava.security.policy=config/codion_server.policy"
    ]
}

distributions {
    main {
        contents {
            from(fileTree(dir: 'config', include: '**/*')) {
                into("bin/config")
            }
        }
    }
}

jlink {
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    mergedModule {
        excludeRequires 'com.fasterxml.jackson.core', 'com.fasterxml.jackson.annotation', 'com.fasterxml.jackson.databind'
    }

    launcher {
        name = 'chinook-server'
        jvmArgs = [
                "-Xmx256m",
                "-Djava.rmi.server.hostname=${serverHost}",
                "-Dcodion.configurationFile=config/h2_embedded.config",
                "-Dlogback.configurationFile=config/logback.xml",
                "-Djava.security.policy=config/codion_server.policy"
        ]
    }

    jpackage {
        installerType = "deb"
        installerOptions = ['--linux-shortcut']
    }
}

apply from: "../extra-module-info-jasperreports.gradle"

extraJavaModuleInfo {
    automaticModule("jersey-container-servlet-${jerseyVersion}.jar", "jersey.container.servlet")
    automaticModule("jersey-container-servlet-core-${jerseyVersion}.jar", "jersey.container.servlet.core")
    automaticModule("jersey-container-jetty-servlet-${jerseyVersion}.jar", "jersey.container.jetty.servlet")
    automaticModule("jersey-container-jetty-http-${jerseyVersion}.jar", "jersey.container.jetty.http")
    automaticModule("jersey-media-jaxb-${jerseyVersion}.jar", "jersey.media.jaxb")
    automaticModule("jersey-common-${jerseyVersion}.jar", "jersey.common")
    automaticModule("jersey-server-${jerseyVersion}.jar", "jersey.server")
    automaticModule("jersey-client-${jerseyVersion}.jar", "jersey.client")
    automaticModule("jersey-hk2-${jerseyVersion}.jar", "jersey.hk2")
    automaticModule("javax.servlet-api-3.1.0.jar", "javax.servlet.api")
    automaticModule("jakarta.inject-2.6.1.jar", "jakarta.inject")
    automaticModule("hk2-locator-2.6.1.jar", "hk2.locator")
    automaticModule("hk2-api-2.6.1.jar", "hk2.api")
    automaticModule("hk2-utils-2.6.1.jar", "hk2.utils")
    automaticModule("aopalliance-repackaged-2.6.1.jar", "aopalliance.repackaged")
    automaticModule("osgi-resource-locator-1.0.3.jar", "osgi.resource.locator")
    automaticModule("javassist-3.25.0-GA.jar", "javassist")
}