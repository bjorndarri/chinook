plugins {
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply from: "../build-shared.gradle"

apply plugin: 'java-library'

dependencies {
    api project(':chinook-domain-api')
    implementation "is.codion.jdk11:codion-common-rmi:${codionVersion}"
    implementation "is.codion.jdk11:codion-framework-db-local:${codionVersion}"

    testImplementation "is.codion.jdk11:codion-framework-domain-test:${codionVersion}"
    testRuntimeOnly "is.codion.jdk11:codion-dbms-h2database:${codionVersion}"
    testRuntimeOnly "com.h2database:h2:1.4.200"

    runtimeOnly "commons-logging:commons-logging:${commonsLoggingVersion}"
    runtimeOnly "commons-digester:commons-digester:${commonsDigesterVersion}"
    runtimeOnly "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
    runtimeOnly "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}"
    runtimeOnly "org.eclipse.jdt:ecj:3.24.0"
}

test {
    systemProperty "codion.test.user", "scott:tiger"
    systemProperty "codion.db.url", "jdbc:h2:mem:h2db"
    systemProperty "codion.db.initScripts", "classpath:create_schema.sql"
}

extraJavaModuleInfo {
    automaticModule("jasperreports-${jasperReportsVersion}.jar", "jasperreports")
    automaticModule("commons-logging-${commonsLoggingVersion}.jar", "commons.logging")
    automaticModule("commons-digester-${commonsDigesterVersion}.jar", "commons.digester")
    automaticModule("commons-collections-${commonsCollectionsVersion}.jar", "commons.collections")
    automaticModule("commons-beanutils-${commonsBeanUtilsVersion}.jar", "commons.beanutils")
    automaticModule("ecj-${ecjVersion}.jar", "ecj")
}

task buildReports(dependsOn: 'compileJava') {
    group 'build'
    def jasperSourceDir = file('src/main/reports')
    def jasperTargetDir = file(sourceSets.main.output.resourcesDir.toString() + '/is/codion/framework/demos/chinook/domain')
    inputs.dir jasperSourceDir
    outputs.dir jasperTargetDir
    doLast {
        ant {
            taskdef(name: 'jrc', classname: 'net.sf.jasperreports.ant.JRAntCompileTask', classpath: configurations.runtimeClasspath.asPath)
            jasperTargetDir.mkdirs()
            jrc(srcdir: jasperSourceDir, destdir: jasperTargetDir) {
                classpath(path: sourceSets.main.output.classesDirs)
                include(name: '**/*.jrxml')
            }
        }
    }
}
classes.finalizedBy buildReports