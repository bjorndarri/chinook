plugins {
    id 'org.beryx.jlink' version "${jlinkVersion}"
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply plugin: 'application'

dependencies {
    implementation project(':chinook-client')

    runtimeOnly "is.codion.jdk11:codion-framework-db-rmi:${version}"
}

apply from: "../extra-module-info-jasperreports.gradle"

application {
    mainModule = "is.codion.framework.demos.chinook.client"
    getMainClass().set("is.codion.framework.demos.chinook.ui.ChinookAppPanel")
    applicationDefaultJvmArgs = [
            //add-reads for accessing default methods for Entity types
            "--add-reads", "is.codion.framework.domain=is.codion.framework.demos.chinook.domain.api",
            "-Dcodion.client.connectionType=remote",
            "-Dcodion.server.hostname=${serverHost}",
            "-Dcodion.server.registryPort=${serverRegistryPort}",
            "-Djavax.net.ssl.trustStore=truststore.jks",
            "-Djavax.net.ssl.trustStorePassword=crappypass",
            "-Dsun.awt.disablegrab=true"
    ]
}

jlink {
    imageName = project.name + "-" + project.version
    moduleName = application.mainModule
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules',
               'is.codion.framework.db.rmi,is.codion.plugin.logback.proxy']

    launcher {
        name = 'chinook-remote'
        jvmArgs = [
                "-Dcodion.client.connectionType=remote",
                "-Dcodion.server.hostname=${serverHost}",
                "-Dcodion.server.registryPort=${serverRegistryPort}",
                "-Djavax.net.ssl.trustStore=truststore.jks",
                "-Djavax.net.ssl.trustStorePassword=crappypass",
                "--add-reads", "is.codion.swing.framework.model=chinook.merged.module"//for json
        ]
    }

    jpackage {
        imageName = "Chinook-Remote"
        appVersion = project.version.replace('-SNAPSHOT', '')
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = 'deb'
            icon = "../chinook.png"
            installerOptions = [
                    '--resource-dir', 'build/jpackage/Chinook-Remote/lib',
                    '--linux-shortcut'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            icon = "../chinook.ico"
            installerOptions = [
                    '--win-menu',
                    '--win-shortcut'
            ]
        }
    }
}