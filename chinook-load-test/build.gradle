plugins {
    id "de.jjohannes.extra-java-module-info" version "0.6"
}

apply from: "../build-shared.gradle"

apply plugin: 'application'

dependencies {
    implementation project(':chinook-domain-api')
    implementation project(':chinook-client')

    implementation "is.codion.jdk11:codion-swing-common-tools-ui:${codionVersion}"
    implementation "is.codion.jdk11:codion-swing-framework-ui:${codionVersion}"
    implementation "is.codion.jdk11:codion-swing-framework-tools:${codionVersion}"

    runtimeOnly "is.codion.jdk11:codion-framework-db-rmi:${codionVersion}"
    runtimeOnly "is.codion.jdk11:codion-framework-db-http:${codionVersion}"
}

extraJavaModuleInfo {
    automaticModule("jasperreports-6.16.0.jar", "jasperreports")
    automaticModule("commons-logging-1.2.jar", "commons.logging")
    automaticModule("commons-digester-2.1.jar", "commons.digester")
    automaticModule("commons-collections-3.2.2.jar", "commons.collections")
    automaticModule("commons-beanutils-1.9.4.jar", "commons.beanutils")
    automaticModule("ecj-3.24.0.jar", "ecj")
    automaticModule("radiance-substance-3.5.1.jar", "radiance.substance")
    automaticModule("radiance-substance-extras-3.5.1.jar", "radiance.substance.extras")
    automaticModule("radiance-trident-3.5.1.jar", "radiance.trident")
    automaticModule("radiance-neon-3.5.1.jar", "radiance.neon")
}

application {
    mainModule = "is.codion.framework.demos.chinook.client.loadtest"
    mainClassName = "is.codion.framework.demos.chinook.client.loadtest.ChinookLoadTest"
}

task runRmi(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHost}",
            'codion.server.registryPort'      : "${serverRegistryPort}",
            'javax.net.ssl.trustStore'        : '../chinook-server/config/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass'
    ]
}

task runHttp(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHost}",
            'codion.client.http.port'     : "${serverHttpPort}"
    ]
}