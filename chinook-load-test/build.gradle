plugins {
    id 'org.beryx.jlink' version "${jlinkVersion}"
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply plugin: 'application'

dependencies {
    implementation project(':chinook-domain-api')
    implementation project(':chinook-client')

    implementation "is.codion.jdk11:codion-swing-common-tools-ui:${version}"
    implementation "is.codion.jdk11:codion-swing-framework-ui:${version}"
    implementation "is.codion.jdk11:codion-swing-framework-tools:${version}"

    runtimeOnly "is.codion.jdk11:codion-framework-db-rmi:${version}"
}

apply from: "../extra-module-info-jasperreports.gradle"

applicationDefaultJvmArgs = [
        "-Xmx2048m",
        "-Dcodion.client.connectionType=remote",
        "-Dcodion.server.hostname=${serverHost}",
        "-Dcodion.server.registryPort=${serverRegistryPort}",
        "-Dcodion.client.connectionType=remote",
        "-Djavax.net.ssl.trustStore=truststore.jks",
        "-Djavax.net.ssl.trustStorePassword=crappypass",
        "--add-reads", "is.codion.swing.framework.model=chinook.merged.module"//for json
]

application {
    mainModule = "is.codion.framework.demos.chinook.client.loadtest"
    getMainClass().set("is.codion.framework.demos.chinook.client.loadtest.ChinookLoadTest")
}

jlink {
    imageName = project.name
    moduleName = application.mainModule
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules',
               'is.codion.framework.db.rmi,is.codion.plugin.logback.proxy']

    launcher {
        name = 'chinook-load-test'
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "Chinook-Load-Test"
        appVersion = project.version.replace('-SNAPSHOT', '')
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = 'deb'
            icon = "../chinook.png"
            installerOptions = [
                    '--resource-dir', 'build/jpackage/Chinook-Load-Test/lib',
                    '--linux-shortcut'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            icon = "../chinook.ico"
            installerOptions = [
                    '--win-menu',
                    '--win-shortcut'
            ]
        }
    }
}