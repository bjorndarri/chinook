plugins {
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply from: "../build-shared.gradle"

apply plugin: 'application'

dependencies {
    implementation project(':chinook-domain-api')
    implementation project(':chinook-client')

    implementation "is.codion.jdk11:codion-swing-common-tools-ui:${codionVersion}"
    implementation "is.codion.jdk11:codion-swing-framework-ui:${codionVersion}"
    implementation "is.codion.jdk11:codion-swing-framework-tools:${codionVersion}"

    runtimeOnly "is.codion.jdk11:codion-framework-db-rmi:${codionVersion}"
    runtimeOnly "is.codion.jdk11:codion-framework-db-http:${codionVersion}"
}

extraJavaModuleInfo {
    automaticModule("jasperreports-${jasperReportsVersion}.jar", "jasperreports")
    automaticModule("commons-logging-${commonsLoggingVersion}.jar", "commons.logging")
    automaticModule("commons-digester-${commonsDigesterVersion}.jar", "commons.digester")
    automaticModule("commons-collections-${commonsCollectionsVersion}.jar", "commons.collections")
    automaticModule("commons-beanutils-${commonsBeanUtilsVersion}.jar", "commons.beanutils")
    automaticModule("ecj-${ecjVersion}.jar", "ecj")
    automaticModule("radiance-substance-${radianceVersion}.jar", "radiance.substance")
    automaticModule("radiance-substance-extras-${radianceVersion}.jar", "radiance.substance.extras")
    automaticModule("radiance-trident-${radianceVersion}.jar", "radiance.trident")
    automaticModule("radiance-neon-${radianceVersion}.jar", "radiance.neon")
}

application {
    mainModule = "is.codion.framework.demos.chinook.client.loadtest"
    mainClassName = "is.codion.framework.demos.chinook.client.loadtest.ChinookLoadTest"
}

task runRmi(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : "${serverHost}",
            'codion.server.registryPort'      : "${serverRegistryPort}",
            'javax.net.ssl.trustStore'        : '../chinook-server/config/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass'
    ]
}

task runHttp(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType': 'http',
            'codion.client.http.secure'   : 'false',
            'codion.client.http.hostname' : "${serverHost}",
            'codion.client.http.port'     : "${serverHttpPort}"
    ]
}