plugins {
    id "org.beryx.jlink"
    id "chinook.jasperreports"
}

dependencies {
    implementation project(":chinook-load-test")

    runtimeOnly(libs.codion.framework.db.http)
}

applicationDefaultJvmArgs = [
        "-Xmx1024m",
        "-Dcodion.client.connectionType=http",
        "-Dcodion.client.http.secure=false",
        "-Dcodion.client.http.hostname=${serverHost}",
        "-Dcodion.client.http.port=${serverHttpPort}"
]

application {
    mainModule.set("is.codion.framework.demos.chinook.client.loadtest")
    mainClass.set("is.codion.framework.demos.chinook.client.loadtest.ChinookLoadTest")
}

jlink {
    imageName.set(project.name)
    moduleName.set(application.mainModule)
    options = ["--strip-debug", "--no-header-files", "--no-man-pages", "--add-modules",
               "is.codion.framework.db.http,is.codion.plugin.logback.proxy"]

    addExtraDependencies("jackson-core", "jackson-databind", "jackson-annotation")

    launcher {
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "Chinook-Load-Test-Http"
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            icon = "../chinook.png"
            installerOptions = [
                    "--resource-dir", "build/jpackage/Chinook-Load-Test-Http/lib",
                    "--linux-shortcut"
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = "msi"
            icon = "../chinook.ico"
            installerOptions = [
                    "--win-menu",
                    "--win-shortcut"
            ]
        }
    }
}