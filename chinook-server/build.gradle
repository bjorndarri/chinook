plugins {
    id 'org.beryx.jlink' version "${jlinkVersion}"
    id "de.jjohannes.extra-java-module-info" version "${extraJavaModuleInfoVersion}"
}

apply plugin: 'application'

dependencies {
    runtimeOnly "is.codion.jdk11:codion-framework-server:${version}"

    runtimeOnly project(":chinook-domain")

    runtimeOnly "is.codion.jdk11:codion-plugin-hikari-pool:${version}"
    runtimeOnly "is.codion.jdk11:codion-dbms-h2database:${version}"
    runtimeOnly "com.h2database:h2:1.4.200"

    //logging library, skipping the email stuff
    runtimeOnly ("is.codion.jdk11:codion-plugin-logback-proxy:${version}") {
        exclude group: 'com.sun.mail', module: 'javax.mail'
    }
}

apply from: "../extra-module-info-jasperreports.gradle"

applicationDefaultJvmArgs = [
        "-Xmx256m",
        //add-reads for accessing default methods for Entity types
        "--add-reads", "is.codion.framework.domain=is.codion.framework.demos.chinook.domain.api",
        "-Djava.rmi.server.hostname=${serverHost}",
        "-Dcodion.configurationFile=classpath:h2_embedded.config",
        "-Dlogback.configurationFile=logback.xml"
]

application {
    mainModule = "is.codion.framework.server"
    getMainClass().set("is.codion.framework.server.EntityServer")
}

jlink {
    imageName = project.name
    moduleName = application.mainModule
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules',
               'is.codion.framework.db.local,is.codion.dbms.h2database,is.codion.plugin.hikari.pool,' +
                       'is.codion.plugin.logback.proxy,is.codion.framework.demos.chinook.domain']

    launcher {
        name = 'chinook-server'
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "Chinook-Server"
        appVersion = project.version.replace('-SNAPSHOT', '')
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = 'deb'
            icon = "../chinook.png"
            installerOptions = [
                    '--resource-dir', 'build/jpackage/Chinook-Server/lib',
                    '--linux-shortcut'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            icon = "../chinook.ico"
            imageOptions += ['--win-console']
            installerOptions = [
                    '--win-menu',
                    '--win-shortcut'
            ]
        }
    }
}

prepareMergedJarsDir.doLast {
    copy {
        from 'src/main/resources'
        into "$jlinkBasePath/mergedjars"
    }
}