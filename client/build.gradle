apply from: "../build-shared.gradle"

apply plugin: 'application'

dependencies {
    implementation project(':domain-api')

    implementation "is.codion.jdk11:codion-framework-db-local:${codionVersion}"
    implementation "is.codion.jdk11:codion-swing-framework-ui:${codionVersion}"
    implementation "is.codion.jdk11:codion-plugin-imagepanel:${codionVersion}"
    implementation "is.codion.jdk11:codion-plugin-ikonli-foundation:${codionVersion}"
    //todo required for running with local connection
    //runtimeOnly project(':domain')
    runtimeOnly "is.codion.jdk11:codion-framework-db-rmi:${codionVersion}"
    runtimeOnly "is.codion.jdk11:codion-framework-db-http:${codionVersion}"
    runtimeOnly "is.codion.jdk11:codion-plugin-logback-proxy:${codionVersion}"
    runtimeOnly "is.codion.jdk11:codion-dbms-h2database:${codionVersion}"
    runtimeOnly "com.h2database:h2:1.4.200"
    testRuntimeOnly project(':domain')
}

test {
    systemProperty "codion.db.url", "jdbc:h2:mem:h2db"
    systemProperty "codion.db.initScripts", "classpath:create_schema.sql"
}

application {
    mainModule = "is.codion.framework.demos.chinook.client"
    mainClassName = "is.codion.framework.demos.chinook.ui.ChinookAppPanel"
    applicationDefaultJvmArgs = [
            //add-reads for accessing default methods for Entity types
            "--add-reads", "is.codion.framework.domain=is.codion.framework.demos.chinook.domain.api",
            "-Dcodion.client.connectionType=local",
            "-Dcodion.db.url=jdbc:h2:mem:h2db",
            "-Dcodion.db.initScripts=classpath:create_schema.sql",
            "-Dsun.awt.disablegrab=true"
    ]
}

task runRmi(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType'    : 'remote',
            'codion.server.hostname'          : 'localhost',
            'javax.net.ssl.trustStore'        : '../server/src/main/config/truststore.jks',
            'javax.net.ssl.trustStorePassword': 'crappypass',
            'sun.awt.disablegrab'             : 'true'
    ]
}

task runHttp(type: JavaExec) {
    group 'application'
    main = application.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    systemProperties = [
            'codion.client.connectionType' : 'http',
            'codion.client.http.secure'    : 'false',
            'codion.client.http.hostname'  : 'localhost',
            'sun.awt.disablegrab'          : 'true'
    ]
}