plugins {
    id "chinook.server-monitor"
}

dependencies {
    runtimeOnly(libs.codion.tools.server.monitor)
    runtimeOnly(libs.codion.plugin.logback.proxy)
}

applicationDefaultJvmArgs = [
        "-Xmx512m",
        "-Dcodion.server.hostname=${serverHost}",
        "-Dcodion.server.registryPort=${serverRegistryPort}",
        "-Dcodion.client.trustStore=truststore.jks",
        "-Dcodion.client.trustStorePassword=crappypass",
        "-Dlogback.configurationFile=logback.xml",
        "-Dcodion.server.admin.user=scott:tiger"
]

application {
    mainModule.set("is.codion.tools.server.monitor")
    mainClass.set("is.codion.tools.server.monitor.ui.EntityServerMonitorPanel")
}

jlink {
    imageName.set(project.name)
    moduleName.set(application.mainModule)
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages", "--add-modules",
               "java.naming,is.codion.plugin.logback.proxy"]

    launcher {
        jvmArgs = applicationDefaultJvmArgs
        jvmArgs += ["--add-reads", "chinook.merged.module=org.slf4j"]//for sslcontext-kickstart
    }

    jpackage {
        imageName = "Chinook-Server-Monitor"
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            icon = "../chinook.png"
            installerOptions = [
                    "--resource-dir", "build/jpackage/Chinook-Server-Monitor/lib",
                    "--linux-shortcut"
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = "msi"
            icon = "../chinook.ico"
            installerOptions = [
                    "--win-menu",
                    "--win-shortcut"
            ]
        }
    }
}

prepareMergedJarsDir.doLast {
    copy {
        from "src/main/resources"
        into "$jlinkBasePath/mergedjars"
    }
}